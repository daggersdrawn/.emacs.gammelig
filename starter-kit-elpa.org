#+TITLE: Starter Kit ELPA
#+OPTIONS: toc:nil num:nil ^:nil

This is part of the [[file:starter-kit.org][Emacs Starter Kit]].

* Starter Kit ELPA
[[http://tromey.com/elpa/][Emacs List Package Archive]] (ELPA)

The following packages should all ways be installed

#+begin_src emacs-lisp
  (defvar starter-kit-packages (list 'idle-highlight
                                     'ruby-mode
                                     'inf-ruby
                                     'js2-mode
                                     'css-mode
                                     'yasnippet-bundle
                                     'gist
                                     'paredit)
    "Libraries that should be installed by default.")
#+end_src

#+begin_src emacs-lisp
(defun starter-kit-elpa-install ()
  "Install all starter-kit packages that aren't installed."
  (interactive)
  (dolist (package starter-kit-packages)
    (unless (or (member package package-activated-list)
                (functionp package))
      (message "Installing %s" (symbol-name package))
      (package-install package))))
#+end_src

#+begin_src emacs-lisp
(defun esk-online? ()
  "See if we're online.

Windows does not have the network-interface-list function, so we
just have to assume it's online."
  ;; TODO how could this work on Windows?
  (if (and (functionp 'network-interface-list)
           (network-interface-list))
      (some (lambda (iface) (unless (equal "lo" (car iface))
                         (member 'up (first (last (network-interface-info
                                                   (car iface)))))))
            (network-interface-list))
    t))
#+end_src

On your first run, this should pull in all the base packages.
#+begin_src emacs-lisp
(when (esk-online?)
  (unless package-archive-contents (package-refresh-contents))
  (starter-kit-elpa-install))
#+end_src

Workaround for an ELPA bug that people are reporting but I've been
unable to reproduce:
#+begin_src emacs-lisp
(autoload 'paredit-mode "paredit")
#+end_src
